buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.springframework.boot' version "2.3.4.RELEASE"
    id 'io.spring.dependency-management' version "1.0.10.RELEASE"
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.jetbrains.kotlin.plugin.spring' version "$kotlin_version"
    id 'com.google.cloud.tools.jib' version '2.2.0'
    id 'com.github.ben-manes.versions' version '0.33.0'
    id 'org.jlleitschuh.gradle.ktlint' version '9.4.0'
}

group 'digital.cesko'
version '0.0.5'
java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation "org.jetbrains.exposed:exposed-dao:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-java-time:$exposed_version"
    implementation "org.postgresql:postgresql"
    implementation "io.github.microutils:kotlin-logging:2.0.3"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlin_version"
    testImplementation "com.h2database:h2"
    testImplementation "org.awaitility:awaitility"
    testImplementation "net.javacrumbs.json-unit:json-unit-spring:2.19.0"
    testImplementation "org.testcontainers:postgresql:1.14.3"
    testImplementation "net.jadler:jadler-core:1.3.0"
    testImplementation "net.jadler:jadler-jdk:1.3.0"

    // google docs API
    implementation 'com.google.api-client:google-api-client:1.30.9'
    implementation 'com.google.auth:google-auth-library-oauth2-http:0.20.0'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20200508-1.30.9'

    //Lucene engine (for search)
    implementation group: 'org.apache.lucene', name: 'lucene-core', version: "$lucene_version"
    implementation group: 'org.apache.lucene', name: 'lucene-queryparser', version: "$lucene_version"
    implementation group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: "$lucene_version"

    // Apache commons-csv for parsing csv files created by Internet Stream
    implementation "org.apache.commons:commons-csv:1.8"
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}


jib {
    from {
        image = 'openjdk:alpine'
    }
    to {
        image = 'cityvizor/server-kotlin:test'
    }
    container {
        jvmFlags = ['-Xmx256m']
    }
}
